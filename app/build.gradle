import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 59
        versionName "1.0.6"
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW"
    }

    lintOptions {
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
            // Disable default NdkCompile task
            jni.srcDirs = []
            // Set new jni lib dir
            jniLibs.srcDir 'src/main/libs'
        }
    }

    // noinspection GroovyAssignabilityCheck
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = project.plugins.findPlugin('com.android.application').sdkHandler.getNdkFolder()
        if (ndkDir == null) {
            ndkDir = System.getenv('NDK_HOME')
        }
        assert ndkDir != null
        def ndkBuilder
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            ndkBuilder = 'ndk-build.cmd'
        } else {
            ndkBuilder = 'ndk-build'
        }
        commandLine "$ndkDir\\$ndkBuilder",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors()
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

clean {
    delete file('src/main/obj').absolutePath
    delete file('src/main/libs').absolutePath
}

task copyNotice(type: Copy) {
    from '../NOTICE'
    into './src/main/assets'
}

dependencies {
    compile project(':beerbelly')
    compile project(':conaco')
    compile project(':easyrecyclerview')
    compile project(':hotspot')
    compile project(':okhttp3')
    compile project(':rippleold')
    compile project(':yorozuya')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.github.amlcurran.showcaseview:library:5.4.1'
    compile 'com.github.seven332:unifile:0.1.1'
    compile 'com.github.seven332:drawerLayout:0.1.0'
    compile 'com.github.seven332:refreshlayout:0.1.0'
    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.7'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    compile 'org.jsoup:jsoup:1.8.3'
}
