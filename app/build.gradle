import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Disable default NdkCompile task
    // Set new jni lib dir
    sourceSets.main {
        java.srcDirs = ['src/main/java', 'src/main/java-gen']
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    // noinspection GroovyAssignabilityCheck
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = project.plugins.findPlugin('com.android.application').sdkHandler.getNdkFolder()
        def ndkBuilder
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            ndkBuilder = 'ndk-build.cmd'
        } else {
            ndkBuilder = 'ndk-build'
        }
        commandLine "$ndkDir\\$ndkBuilder",
                '-C', file('src/main/jni').absolutePath,
                'NDK_DEBUG=0',
                'NDK_TOOLCHAIN_VERSION=clang3.5',
                '-j4'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

clean {
    delete file('src/main/obj').absolutePath
    delete file('src/main/libs').absolutePath
}

dependencies {
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'de.greenrobot:greendao:1.3.7'
    compile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':unifile')
    compile project(':vectorold')
    compile project(':hotspot')
    compile project(':rippleold')
    compile project(':beerbelly')
    compile project(':conaco')
    compile project(':cardsalon')
    compile project(':httpclient')
    compile project(':yorozuya')
}
