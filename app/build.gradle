import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 47
        versionName "0.6.16.3"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    // Disable default NdkCompile task
    // Set new jni lib dir
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    // noinspection GroovyAssignabilityCheck
    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = project.plugins.findPlugin('com.android.application').sdkHandler.getNdkFolder()
        def ndkBuilder
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            ndkBuilder = 'ndk-build.cmd'
        } else {
            ndkBuilder = 'ndk-build'
        }
        commandLine "$ndkDir\\$ndkBuilder",
                '-C', file('src/main').absolutePath,
                'NDK_DEBUG=0',
                'NDK_TOOLCHAIN_VERSION=clang',
                '-j4'
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

clean {
    delete file('src/main/obj').absolutePath
    delete file('src/main/libs').absolutePath
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':vector')
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
}
